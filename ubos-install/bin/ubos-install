#!/usr/bin/perl
#
# Install UBOS on one or more currently unmounted devices.
#
# Examples:
#     ubos-install --class pc --rootpartition /dev/sda
#     ubos-install --class pc --rootpartition /dev/sda --rootpartition /dev/sdb --hostname rpi
#     ubos-install --class rpi --rootpartition /dev/sdb
#     ubos-install --class pc --bootloader-device /dev/sda --rootpartition /dev/sda1 --varpartition /dev/sda2
#
# This file is part of ubos-admin.
# (C) 2012-2014 Indie Computing Corp.
#
# ubos-admin is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# ubos-admin is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with ubos-admin.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use warnings;

use Getopt::Long qw( GetOptionsFromArray );

use UBOS::Logging;
use UBOS::Utils;
use UBOS::Install::DiskLayout;
use UBOS::Install::Installers::Pc;

if ( $< != 0 ) {
    fatal( "This command must be run as root" ); 
}

my $verbose          = 0;
my $logConfigFile    = undef;
my $ret              = 0;
my $channel          = undef;
my $hostname         = undef;
my $deviceclassname  = undef;
my $target           = undef;
my $repo             = undef;
my $bootloaderdevice = undef;
my $bootpartition    = undef;
my @varpartitions    = ();
my $help             = 0;

my $parseOk = GetOptions(
        'verbose+'           => \$verbose,
        'logConfig=s'        => \$logConfigFile,
        'channel=s'          => \$channel,
        'hostname=s'         => \$hostname,
        'deviceclass=s'      => \$deviceclassname,
        'target=s'           => \$target,
        'repository=s'       => \$repo,
        'bootloaderdevice=s' => \$bootloaderdevice,
        'bootpartition=s'    => \$bootpartition,
        'varpartition=s'     => \@varpartitions,
        'help'                => \$help );

UBOS::Logging::initialize( 'ubos-install', undef, $verbose, $logConfigFile );

if( $help ) {
    synopsisHelpQuit( 1 );
}

my @rootpartitions = @ARGV;

if( !$parseOk || !@rootpartitions ) {
    synopsisHelpQuit( 0 );
}
if( $target && ! -d $target ) {
    fatal( 'Target is not a directory:', $target );
}
unless( $deviceclassname ) {
    # Use current device class
    my $out;
    UBOS::Utils::myexec( 'uname -a', undef, \$out, undef );
    if( $out =~ m!(alarmpi|raspberry).*armv6l! ) {
        $deviceclassname = 'rpi';
    } elsif( $out =~ m!x86_64! ) {
        $deviceclassname = 'pc';
    } else {
        fatal( 'Cannot automatically determine --deviceclass. Need to specify as argument.' );
    }
}

my $installer = findInstaller( $deviceclassname );
unless( $installer ) {
    fatal( 'Cannot find installer for device class', $deviceclassname );
}

if( $target ) {
    $installer->setTarget( $target );
} else {
    $installer->useTempTarget();
}
if( $hostname ) {
    $installer->setHostname( $hostname );
}
if( !$channel && -r '/etc/ubos/channel' ) {
    $channel = UBOS::Utils::slurpFile( '/etc/ubos/channel' );
    $channel =~ s!^\s+!!;
    $channel =~ s!\s+$!!;
}
if( $channel ) {
    $installer->setChannel( $channel );
}
if( $repo ) {
    $installer->setRepo( $repo );
}

debug( 'Using installer', $installer );

my $diskLayout = UBOS::Install::DiskLayout->new(
        \@rootpartitions,
        \@varpartitions,
        $bootpartition,
        $bootloaderdevice );

$installer->install( $diskLayout );

exit( $ret );


##
# Find the right installer for this device class
sub findInstaller {
    my $className = shift;

    my $installers = UBOS::Utils::findPerlShortModuleNamesInPackage( 'UBOS::Install::Installers' );
    my $installer  = $installers->{$className};

    unless( $installer ) {
        return undef;
    }

    my $ret = UBOS::Utils::invokeMethod( $installer . '->new' );
    return $ret;
}

#####
# Print synopsis or help
# $long: if true, print long help
# $exit: exit code
sub synopsisHelpQuit {
    my $long = shift;
    my $exit = shift || 0;

    if( $long ) {
        print <<END;
Install UBOS.

END
    } else {
        print "Synopsis:\n";
    }
    print "    $0 [ --verbose | --logConfFile <log4perlconf> ][ --channel <channel> ][ --hostname <hostname> ][ --repository <repo> ]";
    print " [ --target <target> ][ --bootloaderdevice <device> ][ --bootpartition <device> ][ --varpartition <device> ]...";
    print " --deviceclass <deviceclass> <device> ...\n";

    if( $long ) {
        print <<END;
        Install UBOS on <device>.
        channel:          the UBOS release channel; defaults to green
        hostname:         the hostname to set; defaults to a deviceclass-specific name
        repository:       use this local UBOS repository instead of http://depot.ubos.net/
        bootloaderdevice: the device which will contain the boot loader. Only needed
                          on x86_64 architecture. Defaults to the root partition
        bootpartition:    the device which will contain the /boot partition. If not
                          specified, /boot will be created on the root device
        varpartition:     the device which will contain the /var partition. If more
                          than one device is specified, RAID 1 will be set up across
                          all the devices. If not specified, /var will be created on
                          the root device.
        target:           the directory to which the new system will be mounted
                          temporarily during assembly. Defaults to temp directory
                          in current working directory
        rootpartition:    the device, or image file, which will contain the root
                          partition. If more than one device is specified, RAID 1 will
                          be set up across all the devices.
        deviceclass:      the device class, e.g. rpi (Raspberry Pi), pc (x86_64)

END
    }
    print "    $0 --help\n";
    if( $long ) {
        print <<END;
        Display help text.

END
    }

    exit 0;
}

1;
